/-
Copyright (c) 2025 Rémy Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rémy Degenne
-/
module

public import LeanMachineLearning.Online.Bandit.Regret
public import LeanMachineLearning.SequentialLearning.Algorithms.RoundRobin
public import LeanMachineLearning.SequentialLearning.SumRewards
public import LeanMachineLearning.ForMathlib.MeasureTheory.Order.MeasurableArg

/-! # The Explore-Then-Commit Algorithm

-/

@[expose] public section

open MeasureTheory ProbabilityTheory Finset Learning
open scoped ENNReal NNReal

namespace Bandits

variable {K : ℕ}

section AlgorithmDefinition

variable (K) in
/-- Arm pulled by the ETC algorithm at time `n`, as a function of the history before `n`.
For `n < K * m`, this is arm `n % K`.
For `n = K * m`, this is the arm with the highest empirical mean after the exploration phase.
For `n > K * m`, this is the same arm as at time `n - 1`. -/
noncomputable
def ETC.nextArm [NeZero K] (m n : ℕ) (h : Hist Unit (Fin K) ℝ n) : Fin K :=
  if hn : n < K * m then RoundRobin.nextAction K n
  else
    if hn_eq : n = K * m then argmax (empMean' n h)
    else (h ⟨n - 1, by omega⟩).action

/-- The next arm pulled by ETC is chosen in a measurable way. -/
@[fun_prop]
lemma ETC.measurable_nextArm [NeZero K] (m n : ℕ) : Measurable (nextArm K m n) := by
  unfold nextArm
  split_ifs <;> fun_prop

variable (K) in
/-- The Explore-Then-Commit algorithm: deterministic algorithm that chooses the next arm according
to `ETC.nextArm`. -/
noncomputable
def etcAlgorithm [NeZero K] (m : ℕ) : Algorithm Unit (Fin K) ℝ :=
  detAlgorithm (fun n p ↦ ETC.nextArm K m n p.1) (by fun_prop)

end AlgorithmDefinition

namespace ETC

variable [NeZero K] {m : ℕ} {ν : Kernel (Fin K) ℝ} [IsMarkovKernel ν]
  {Ω : Type*} {mΩ : MeasurableSpace Ω}
  {P : Measure Ω} [IsProbabilityMeasure P]
  {O : ℕ → Ω → Unit} {A : ℕ → Ω → Fin K} {R : ℕ → Ω → ℝ}

/-- Before round `K * m`, the ETC algorithm behaves like the Round-Robin algorithm. -/
lemma isAlgEnvSeqUntil_roundRobinAlgorithm
    (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P) :
    IsAlgEnvSeqUntil O A R (roundRobinAlgorithm K) (stationaryEnv ν) P (K * m) := by
  refine h.isAlgEnvSeqUntil_of_policy_eq fun n hn ↦ ?_
  simp only [roundRobinAlgorithm, detAlgorithm_policy, etcAlgorithm]
  congr 1 with p
  simp [ETC.nextArm, hn]

section AlgorithmBehavior

lemma arm_ae_eq_nextArm (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P) (n : ℕ) :
    A n =ᵐ[P] fun ω ↦ nextArm K m n (history O A R n ω) :=
  h.action_detAlgorithm_ae_eq n

/-- For `n < K * m`, the arm pulled at time `n` is the arm `n % K`. -/
lemma arm_of_lt (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P)
    {n : ℕ} (hn : n < K * m) :
    A n =ᵐ[P] fun _ ↦ RoundRobin.nextAction K n :=
  RoundRobin.action_ae_eq n ((isAlgEnvSeqUntil_roundRobinAlgorithm h).mono hn)

/-- The arm pulled at time `K * m` is the arm with the highest empirical mean after the exploration
phase. -/
lemma arm_mul
    (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P) :
    A (K * m) =ᵐ[P] fun ω ↦ argmax (empMean' (K * m) (history O A R (K * m) ω)) := by
  filter_upwards [arm_ae_eq_nextArm h (K * m)] with ω hn_eq
  rw [hn_eq, nextArm, dite_eq_right (by simp), dite_eq_left rfl]

/-- For `n ≥ K * m`, the arm pulled at time `n + 1` is the same as the arm pulled at time `n`. -/
lemma arm_add_one_of_ge (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P)
    {n : ℕ} (hn : K * m ≤ n) :
    A (n + 1) =ᵐ[P] fun ω ↦ A n ω := by
  filter_upwards [arm_ae_eq_nextArm h (n + 1)] with ω hn_eq
  rw [hn_eq, nextArm, dite_eq_right (by grind), dite_eq_right (by grind)]
  rfl

/-- For `n ≥ K * m`, the arm pulled at time `n` is the same as the arm pulled at time `K * m`. -/
lemma arm_of_ge (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P)
    {n : ℕ} (hn : K * m ≤ n) :
    A n =ᵐ[P] A (K * m) := by
  have h_ae n : K * m ≤ n → A (n + 1) =ᵐ[P] fun ω ↦ A n ω := arm_add_one_of_ge h
  simp_rw [Filter.EventuallyEq, ← ae_all_iff] at h_ae
  filter_upwards [h_ae] with ω h_ae
  induction n, hn using Nat.le_induction with
  | base => rfl
  | succ n hmn h_ind => rw [h_ae n hmn, h_ind]

/-- At time `K * m`, the number of pulls of each arm is equal to `m`. -/
lemma pullCount_mul (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P) (a : Fin K) :
    pullCount A a (K * m) =ᵐ[P] fun _ ↦ m :=
  RoundRobin.pullCount_mul m (isAlgEnvSeqUntil_roundRobinAlgorithm h) a

lemma pullCount_add_one_of_ge (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P)
    (a : Fin K) {n : ℕ} (hn : K * m ≤ n) :
    pullCount A a (n + 1)
      =ᵐ[P] fun ω ↦ pullCount A a n ω + {ω' | A (K * m) ω' = a}.indicator 1 ω := by
  simp_rw [Filter.EventuallyEq, pullCount_add_one]
  filter_upwards [arm_of_ge h hn] with ω h_arm
  congr 3

/-- For `n ≥ K * m`, the number of pulls of each arm `a` at time `n` is equal to `m` plus
`n - K * m` if arm `a` is the best arm after the exploration phase. -/
lemma pullCount_of_ge (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P)
    (a : Fin K) {n : ℕ} (hn : K * m ≤ n) :
    pullCount A a n
      =ᵐ[P] fun ω ↦ m + (n - K * m) * {ω' | A (K * m) ω' = a}.indicator 1 ω := by
  have h_ae n : K * m ≤ n → pullCount A a (n + 1)
      =ᵐ[P] fun ω ↦ pullCount A a n ω + {ω' | A (K * m) ω' = a}.indicator 1 ω :=
    pullCount_add_one_of_ge h a
  simp_rw [Filter.EventuallyEq, ← ae_all_iff] at h_ae
  have h_ae_Km : pullCount A a (K * m) =ᵐ[P] fun _ ↦ m := pullCount_mul h a
  filter_upwards [h_ae_Km, h_ae] with ω h_Km h_ae
  induction n, hn using Nat.le_induction with
  | base => simp [h_Km]
  | succ n hmn h_ind =>
    rw [h_ae n hmn, h_ind, add_assoc, ← add_one_mul]
    congr
    grind

/-- If at time `K * m` the algorithm chooses arm `a`, then the total reward obtained by pulling
arm `a` is at least the total reward obtained by pulling the best arm. -/
lemma sumRewards_bestArm_le_of_arm_mul_eq
    (h : IsAlgEnvSeq O A R (etcAlgorithm K m) (stationaryEnv ν) P) (a : Fin K) (hm : m ≠ 0) :
    ∀ᵐ ω ∂P, A (K * m) ω = a → sumRewards A R (bestArm ν) (K * m) ω ≤
      sumRewards A R a (K * m) ω := by
  filter_upwards [arm_mul h, pullCount_mul h a, pullCount_mul h (bestArm ν)]
    with ω h_arm ha h_best h_eq
  have h_max := isMaxOn_argmax (empMean' (K * m) (history O A R (K * m) ω)) (bestArm ν)
  rw [← h_arm, h_eq] at h_max
  rw [sumRewards_eq_pullCount_mul_empMean, sumRewards_eq_pullCount_mul_empMean, ha, h_best]
  · gcongr
    rwa [empMean_eq_empMean' (O := O), empMean_eq_empMean' (O := O)]
  · simp [ha, hm]
  · simp [h_best, hm]

end AlgorithmBehavior

end ETC

end Bandits
